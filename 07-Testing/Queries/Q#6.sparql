PREFIX : <https://joinup.ec.europa.eu/collection/rpam/1.1.0/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ccts: <http://www.unece.org/cefact#>
PREFIX cc: <https://joinup.ec.europa.eu/collection/rpam/1.0.0/thesauri/countrycode#>
PREFIX ccev: <https://joinup.ec.europa.eu/collection/semic/ccev/2.0.3/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

# USER STORY 3: I, as the officer responsible for the quality of the data kept in our eMandate Registry, want to see the powers of all mandators and mandatees, so I can compare the powers and constraints of both mandators and mandatees.

select ?Mandate ?MandatorName ?Power ?MandateeName ?MandatorConstraint ?MandateeConstraint
FROM <https://joinup.ec.europa.eu/collection/rpam/1.1.0/mandates#>
where {
    ?Mandate a :Mandate ;
             :empowerment ?Empowerment .
    ?Empowerment :powerOf ?Mandator ;
                 :power [:powerType ?Power] ;
                 :powerAssignment ?PowerAssignment .
    ?PowerAssignment :assignedTo ?Mandatee

    OPTIONAL {?Empowerment :constraint ?MandatorConstraint }
    OPTIONAL {?PowerAssignment :constraint ?MandateeConstraint }
    ?Mandator :givenName ?MorGivenName ;
              :familyName ?MorFamilyName .
    ?Mandatee :givenName ?MeeGivenName ;
              :familyName ?MeeFamilyName
    bind(concat(?MorGivenName, " ", ?MorFamilyName) as ?MandatorName)
    bind(concat(?MeeGivenName, " ", ?MeeFamilyName) as ?MandateeName)
}
