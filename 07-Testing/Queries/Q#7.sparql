PREFIX : <https://joinup.ec.europa.eu/collection/rpam/1.1.0/ontology#>
PREFIX ccev: <https://joinup.ec.europa.eu/collection/semic/ccev/2.0.3/ontology#>
PREFIX cc: <https://joinup.ec.europa.eu/collection/rpam/1.0.0/thesauri/countrycode#>
PREFIX ccts: <http://www.unece.org/cefact#>

# USER STORY 3: I, as the officer responsible for the quality of the data kept in our eMandate Registry, I want to compare Francisco's Constraint-2 with the Constraint-4 that Francisco applied to his two mandatees, so  I can check that they are coherent (and I observe that they are not!)

select ?constraint where {
    {
    :CONSTRAINT-2 ccev:group ?MandatorConstraintGroup .
    ?MandatorConstraintGroup ccev:property [ccev:name ?MandatorConstraint ;
                             ccev:constraint [ccev:datumAmount ?MandatorAmount; ccts:currencyID ?MandatorCurrency]]
    bind(concat("MANDATOR: ", str(?MandatorConstraint), ": ", str(?MandatorAmount), " ", str(?MandatorCurrency)) as ?constraint)
	} UNION
    {
    :CONSTRAINT-4 ccev:group ?g1 .
        ?g1 ccev:group ?g11 .
        ?g11 ccev:property [ccev:name ?g11PropertyName; ccev:constraint [ccev:datumAmount [ccts:amountValue ?g11Value; ccts:currencyID ?g11Currency]]]
        bind(concat("MANDATEE: ", str(?g11PropertyName), ": ", str(?g11Value), " ", str(?g11Currency) ) as ?constraint)

    }
}
