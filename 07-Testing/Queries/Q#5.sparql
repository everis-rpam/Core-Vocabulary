PREFIX : <https://joinup.ec.europa.eu/collection/rpam/1.1.0/ontology#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX ccts: <http://www.unece.org/cefact#>
PREFIX cc: <https://joinup.ec.europa.eu/collection/rpam/1.0.0/thesauri/countrycode#>
PREFIX ccev: <https://joinup.ec.europa.eu/collection/semic/ccev/2.0.3/ontology#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

# USER STORY 2: I, as the officer responsible for the quality of the data kept in our eMandate Registry, want to see the powers of the mandators and mandatees assigned for one specific mandate, so I can compare the powers and constraints of both mandators and mandatees.

select ?MandatorName ?Power ?MandateeName ?MandatorConstraint ?MandateeConstraint
FROM <https://joinup.ec.europa.eu/collection/rpam/1.1.0/mandates#>
where {
    ?Mandate a :Mandate ;
             :id [ccts:identifierValue ?MandateId] ;
             :empowerment ?Empowerment .
    filter (?MandateId = "https://github.com/everis-rpam/RPaM-Ontology/blob/v1.1.0/07-Testing/Mandates/Mandates-Dataset-01.rt.ttl#MANDATE-3")
    ?Empowerment :powerOf ?Mandator ;
                 :power [:powerType ?Power] ;
                 :powerAssignment ?PowerAssignment .
    ?PowerAssignment :assignedTo ?Mandatee

    OPTIONAL {?Empowerment :constraint ?MandatorConstraint }
    OPTIONAL {?PowerAssignment :constraint ?MandateeConstraint }
    ?Mandator :givenName ?MorGivenName ;
              :familyName ?MorFamilyName .
    ?Mandatee :givenName ?MeeGivenName ;
              :familyName ?MeeFamilyName
    bind(concat(?MorGivenName, " ", ?MorFamilyName) as ?MandatorName)
    bind(concat(?MeeGivenName, " ", ?MeeFamilyName) as ?MandateeName)
}
